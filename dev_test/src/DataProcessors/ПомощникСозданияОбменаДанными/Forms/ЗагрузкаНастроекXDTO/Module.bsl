
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УзелОбмена = Параметры.УзелОбмена;
	
	Заголовок = НСтр("ru = 'Загрузка параметров обмена данными'");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ПанельОсновная.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	Элементы.ФормаКомандаГотово.КнопкаПоУмолчанию = Ложь;
	Элементы.ФормаКомандаГотово.Доступность = Ложь;
	
	ПодключитьОбработчикОжидания("ПриНачалеЗагрузкиНастроекXDTO", 1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаГотово(Команда)
	
	Закрыть("Ошибка");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриНачалеЗагрузкиНастроекXDTO()
	
	ПродолжитьОжидание = Истина;
	ПриНачалеЗагрузкиНастроекXDTOНаСервере(ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
			ПараметрыОбработчикаОжидания);
			
		ПодключитьОбработчикОжидания("ПриОжиданииЗагрузкиНастроекXDTO",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	Иначе
		ПриЗавершенииЗагрузкиНастроекXDTO();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОжиданииЗагрузкиНастроекXDTO()
	
	ПродолжитьОжидание = Ложь;
	ПриОжиданииЗагрузкиНастроекXDTOНаСервере(ПараметрыОбработчика, ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		
		ПодключитьОбработчикОжидания("ПриОжиданииЗагрузкиНастроекXDTO",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	Иначе
		ПараметрыОбработчикаОжидания = Неопределено;
		ПриЗавершенииЗагрузкиНастроекXDTO();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииЗагрузкиНастроекXDTO()
	
	СообщениеОбОшибке = "";
	НастройкиЗагружены = Ложь;
	ПриЗавершенииЗагрузкиНастроекXDTOНаСервере(НастройкиЗагружены, СообщениеОбОшибке);
	
	Если НастройкиЗагружены Тогда
		Закрыть("Успешно");
	Иначе
		Элементы.ПанельОсновная.ТекущаяСтраница = Элементы.СтраницаОшибка;
		Элементы.ФормаКомандаГотово.КнопкаПоУмолчанию = Истина;
		Элементы.ФормаКомандаГотово.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриНачалеЗагрузкиНастроекXDTOНаСервере(ПродолжитьОжидание)
	
	НастройкиЗагрузки = Новый Структура;
	НастройкиЗагрузки.Вставить("УзелОбмена", УзелОбмена);
	
	Обработки.ПомощникСозданияОбменаДанными.ПриНачалеЗагрузкиНастроекXDTO(
		НастройкиЗагрузки, ПараметрыОбработчика, ПродолжитьОжидание);
	
КонецПроцедуры
	
&НаСервереБезКонтекста
Процедура ПриОжиданииЗагрузкиНастроекXDTOНаСервере(ПараметрыОбработчика, ПродолжитьОжидание)
	
	ПродолжитьОжидание = Ложь;
	Обработки.ПомощникСозданияОбменаДанными.ПриОжиданииЗагрузкиНастроекXDTO(ПараметрыОбработчика, ПродолжитьОжидание);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗавершенииЗагрузкиНастроекXDTOНаСервере(НастройкиЗагружены, СообщениеОбОшибке = "")
	
	СтатусЗавершения = Неопределено;
	Обработки.ПомощникСозданияОбменаДанными.ПриЗавершенииЗагрузкиНастроекXDTO(
		ПараметрыОбработчика, СтатусЗавершения);
		
	Если СтатусЗавершения.Отказ Тогда
		НастройкиЗагружены = Ложь;
		СообщениеОбОшибке = СтатусЗавершения.СообщениеОбОшибке;
		Возврат;
	Иначе
		НастройкиЗагружены = СтатусЗавершения.Результат.НастройкиЗагружены;
			
		Если Не НастройкиЗагружены Тогда
			СообщениеОбОшибке = СтатусЗавершения.Результат.СообщениеОбОшибке;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти
