#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПроверитьВозможностьИспользованияФормы(Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьРеквизитыФормы();
	
	ИнициализироватьСвойстваФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьПорядковыйНомерПерехода(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ТекстПредупреждения = НСтр("ru = 'Прервать настройку параметров подключения для синхронизации данных?'");
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
		ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, "ЗакрытьФормуБезусловно");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособНастройкиПодключенияПриИзменении(Элемент)
	
	ПриИзмененииСпособНастройкиПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьНастройкиПодключенияИзФайлаПриИзменении(Элемент)
	
	ПриИзмененииЗагрузитьНастройкиПодключенияИзФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНастроекПодключенияДляЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НастройкиДиалога = Новый Структура;
	НастройкиДиалога.Вставить("Заголовок", НСтр("ru = 'Выберите файл с настройками подключения'"));
	НастройкиДиалога.Вставить("Фильтр",    НСтр("ru = 'Файл настроек подключения (*.xml)'") + "|*.xml");
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайла(ЭтотОбъект, "ИмяФайлаНастроекПодключенияДляЗагрузки", СтандартнаяОбработка, НастройкиДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНастроекXDTOКорреспондентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НастройкиДиалога = Новый Структура;
	НастройкиДиалога.Вставить("Заголовок", НСтр("ru = 'Выберите файл с настройками корреспондента'"));
	НастройкиДиалога.Вставить("Фильтр",    НСтр("ru = 'Файл настроек корреспондента (*.xml)'") + "|*.xml");
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайла(ЭтотОбъект, "ИмяФайлаНастроекXDTOКорреспондента", СтандартнаяОбработка, НастройкиДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяФайлаНастроекПодключенияДляВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НастройкиДиалога = Новый Структура;
	НастройкиДиалога.Вставить("Режим",          РежимДиалогаВыбораФайла.Сохранение);
	НастройкиДиалога.Вставить("Заголовок",      НСтр("ru = 'Укажите файл для сохранения настроек подключения'"));
	НастройкиДиалога.Вставить("Фильтр",         НСтр("ru = 'Файл настроек подключения (*.xml)'") + "|*.xml");
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайла(ЭтотОбъект, "ИмяФайлаНастроекПодключенияДляВыгрузки", СтандартнаяОбработка, НастройкиДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодключенияВнешнееСоединениеПриИзменении(Элемент)
	
	ПриИзмененииВидПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодключенияИнтернетПриИзменении(Элемент)
	
	ПриИзмененииВидПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодключенияОбычныеКаналыСвязиПриИзменении(Элемент)
	
	ПриИзмененииВидПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидПодключенияПассивныйРежимПриИзменении(Элемент)
	
	ПриИзмененииВидПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиFILEКаталогНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайловогоКаталога(ЭтотОбъект, "ОбычныеКаналыСвязиFILEКаталог", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСоединениеКаталогИнформационнойБазыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ОбработчикВыбораФайловогоКаталога(ЭтотОбъект, "ВнешнееСоединениеКаталогИнформационнойБазы", СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиFILEИспользованиеПриИзменении(Элемент)
	
	ПриИзмененииОбычныеКаналыСвязиFILEИспользование();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиFTPИспользованиеПриИзменении(Элемент)
	
	ПриИзмененииОбычныеКаналыСвязиFTPИспользование();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиEMAILИспользованиеПриИзменении(Элемент)
	
	ПриИзмененииОбычныеКаналыСвязиEMAILИспользование();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСоединениеВариантРаботыИнформационнойБазыФайловыйПриИзменении(Элемент)
	
	ПриИзмененииВнешнееСоединениеВариантРаботыИнформационнойБазы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСоединениеВариантРаботыИнформационнойБазыКлиентСерверныйПриИзменении(Элемент)
	
	ПриИзмененииВнешнееСоединениеВариантРаботыИнформационнойБазы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСоединениеВидАутентификации1СПредприятиеПриИзменении(Элемент)
	
	ПриИзмененииВнешнееСоединениеВидАутентификации();
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСоединениеВидАутентификацииОперационнаяСистемаПриИзменении(Элемент)
	
	ПриИзмененииВнешнееСоединениеВидАутентификации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиАрхивироватьФайлыПриИзменении(Элемент)
	
	ПриИзмененииОбычныеКаналыСвязиАрхивироватьФайлы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиИспользоватьПарольАрхиваПриИзменении(Элемент)
	
	ПриИзмененииОбычныеКаналыСвязиИспользоватьПарольАрхива();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиFTPИспользоватьОграничениеРазмераФайлаПриИзменении(Элемент)
	
	ПриИзмененииОбычныеКаналыСвязиFTPИспользоватьОграничениеРазмераФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбычныеКаналыСвязиEMAILИспользоватьОграничениеРазмераВложенияПриИзменении(Элемент)
	
	ПриИзмененииОбычныеКаналыСвязиEMAILИспользоватьОграничениеРазмераВложения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаДалее(Команда)
	
	ИзменитьПорядковыйНомерПерехода(+1);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазад(Команда)
	
	ИзменитьПорядковыйНомерПерехода(-1);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаГотово(Команда)
	
	ЗакрытьФормуБезусловно = Истина;
	Закрыть(УзелОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаСправка(Команда)
	
	ОткрытьСправкуФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокДоступныхПриложений(Команда)
	
	НачатьПолучениеСпискаПриложенийДляПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыДоступаВИнтернет(Команда)
	
	ОбменДаннымиКлиент.ОткрытьФормуПараметровПроксиСервера();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодробноеОписаниеСинхронизацииДанных(Команда)
	
	ОбменДаннымиКлиент.ОткрытьПодробноеОписаниеСинхронизации(ПодробнаяИнформацияПоОбмену);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеСпискаПриложенийДляПодключения

&НаКлиенте
Процедура НачатьПолучениеСпискаПриложенийДляПодключения()
	
	Элементы.ПанельПриложенияВСервисе.Видимость = Истина;
	Элементы.ПриложенияВСервисе.Доступность = Ложь;
	Элементы.ПриложенияВСервисеОбновитьСписокДоступныхПриложений.Доступность = Ложь;
	
	Элементы.ПанельПриложенияВСервисе.ТекущаяСтраница = Элементы.СтраницаПриложенияВСервисеОжидание;
	ПодключитьОбработчикОжидания("ПриНачалеПолученияСпискаПриложенийДляПодключения", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеПолученияСпискаПриложенийДляПодключения()
	
	ПараметрыОбработчикаПолученияСпискаПриложений = Неопределено;
	ПродолжитьОжидание = Ложь;
	
	ПриНачалеПолученияСпискаПриложенийДляПодключенияНаСервере(
		ПараметрыОбработчикаПолученияСпискаПриложений, ПродолжитьОжидание);
		
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
			ПараметрыОбработчикаОжиданияПолученияСпискаПриложений);
			
		ПодключитьОбработчикОжидания("ПриОжиданииПолученияСпискаПриложенийДляПодключения",
			ПараметрыОбработчикаОжиданияПолученияСпискаПриложений.ТекущийИнтервал, Истина);
	Иначе
		ПриЗавершенииПолученияСпискаПриложенийДляПодключения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОжиданииПолученияСпискаПриложенийДляПодключения()
	
	ПродолжитьОжидание = Ложь;
	ПриОжиданииПолученияСпискаПриложенийДляПодключенияНаСервере(ПараметрыОбработчикаПолученияСпискаПриложений, ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияПолученияСпискаПриложений);
		
		ПодключитьОбработчикОжидания("ПриОжиданииПолученияСпискаПриложенийДляПодключения",
			ПараметрыОбработчикаОжиданияПолученияСпискаПриложений.ТекущийИнтервал, Истина);
	Иначе
		ПараметрыОбработчикаОжиданияПолученияСпискаПриложений = Неопределено;
		ПриЗавершенииПолученияСпискаПриложенийДляПодключения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПолученияСпискаПриложенийДляПодключения()
	
	Отказ = Ложь;
	ПриЗавершенииПолученияСпискаПриложенийДляПодключенияНаСервере(Отказ);
	
	Элементы.ПриложенияВСервисеОбновитьСписокДоступныхПриложений.Доступность = Истина;
	Элементы.ПриложенияВСервисе.Доступность = Истина;
	
	Если Отказ Тогда
		Элементы.ПанельПриложенияВСервисе.ТекущаяСтраница = Элементы.СтраницаПриложенияВСервисеОшибка;
	Иначе
		Элементы.ПанельПриложенияВСервисе.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриНачалеПолученияСпискаПриложенийДляПодключенияНаСервере(ПараметрыОбработчикаПолученияСпискаПриложений,
		ПродолжитьОжидание)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса") Тогда
		Возврат;
	КонецЕсли;
		
	МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль(
		"Обработки.ПомощникНастройкиСинхронизацииДанныхМеждуПриложениямиВИнтернете");
	
	МодульПомощникНастройки.ПриНачалеПолученияСпискаПриложений(
		ПараметрыОбработчикаПолученияСпискаПриложений, ПродолжитьОжидание);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриОжиданииПолученияСпискаПриложенийДляПодключенияНаСервере(ПараметрыОбработчика, ПродолжитьОжидание)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса") Тогда
		Возврат;
	КонецЕсли;
		
	МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль(
		"Обработки.ПомощникНастройкиСинхронизацииДанныхМеждуПриложениямиВИнтернете");
	
	МодульПомощникНастройки.ПриОжиданииПолученияСпискаПриложений(
		ПараметрыОбработчика, ПродолжитьОжидание);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗавершенииПолученияСпискаПриложенийДляПодключенияНаСервере(Отказ = Ложь)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.РаботаВМоделиСервиса.ОбменДаннымиВМоделиСервиса") Тогда
		Возврат;
	КонецЕсли;
		
	МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль(
		"Обработки.ПомощникНастройкиСинхронизацииДанныхМеждуПриложениямиВИнтернете");
	
	ПараметрыОбработчикаПолученияСпискаПриложений.ДополнительныеПараметры.Вставить("Режим", "НеНастроенныеОбмены");
	ПараметрыОбработчикаПолученияСпискаПриложений.ДополнительныеПараметры.Вставить("ИмяПланаОбмена",
		ИмяПланаОбмена);
	ПараметрыОбработчикаПолученияСпискаПриложений.ДополнительныеПараметры.Вставить("ИдентификаторНастройки",
		ИдентификаторНастройки);
	
	
	СтатусЗавершения = Неопределено;
	МодульПомощникНастройки.ПриЗавершенииПолученияСпискаПриложений(
		ПараметрыОбработчикаПолученияСпискаПриложений, СтатусЗавершения);
		
	Если СтатусЗавершения.Отказ Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТаблицаПриложений = СтатусЗавершения.Результат;
	
	ТаблицаПриложений.Колонки.Добавить("КартинкаРежимИспользования", Новый ОписаниеТипов("Число"));
	ТаблицаПриложений.ЗаполнитьЗначения(1, "КартинкаРежимИспользования"); // приложение в Интернете
	ПриложенияВСервисе.Загрузить(ТаблицаПриложений);
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаПараметровПодключения

&НаКлиенте
Процедура ПриНачалеПроверкиПодключенияOnline()
	
	ПродолжитьОжидание = Истина;
	
	Если ВидПодключения = "Интернет" Тогда
		ПриНачалеПроверкиПодключенияНаСервере("WS", ПродолжитьОжидание);
	ИначеЕсли ВидПодключения = "ВнешнееСоединение" Тогда
		ПриНачалеПроверкиПодключенияНаСервере("COM", ПродолжитьОжидание);
	КонецЕсли;
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
			ПараметрыОбработчикаОжиданияПроверкиПодключения);
			
		ПодключитьОбработчикОжидания("ПриОжиданииПроверкиПодключенияOnline",
			ПараметрыОбработчикаОжиданияПроверкиПодключения.ТекущийИнтервал, Истина);
	Иначе
		ПриЗавершенииПроверкиПодключенияOnline();
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПриОжиданииПроверкиПодключенияOnline()
	
	ПродолжитьОжидание = Ложь;
	ПриОжиданииПроверкиПодключенияНаСервере(ПараметрыОбработчикаПроверкиПодключения, ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияПроверкиПодключения);
		
		ПодключитьОбработчикОжидания("ПриОжиданииПроверкиПодключенияOnline",
			ПараметрыОбработчикаОжиданияПроверкиПодключения.ТекущийИнтервал, Истина);
	Иначе
		ПараметрыОбработчикаОжиданияПроверкиПодключения = Неопределено;
		ПриЗавершенииПроверкиПодключенияOnline();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПроверкиПодключенияOnline()
	
	СообщениеОбОшибке = "";
	ПроверкаПодключенияВыполнена = Ложь;
	ПриЗавершенииПроверкиПодключенияНаСервере(ПроверкаПодключенияВыполнена, СообщениеОбОшибке);
	
	Если ПроверкаПодключенияВыполнена Тогда
		ИзменитьПорядковыйНомерПерехода(+1);
	Иначе
		ИзменитьПорядковыйНомерПерехода(-1);
		
		Если Не ПустаяСтрока(СообщениеОбОшибке) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не удалось подключиться к программе. Проверьте настройки подключения.'"));
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПриНачалеПроверкиПодключенияOffline()
	
	Если ОчередьПроверкиПодключенияОбычныеКаналыСвязи = Неопределено Тогда	
		
		ОчередьПроверкиПодключенияОбычныеКаналыСвязи = Новый Структура;
		
		Если ОбычныеКаналыСвязиFILEИспользование Тогда
			ОчередьПроверкиПодключенияОбычныеКаналыСвязи.Вставить("FILE");
		КонецЕсли;
		
		Если ОбычныеКаналыСвязиFTPИспользование Тогда
			ОчередьПроверкиПодключенияОбычныеКаналыСвязи.Вставить("FTP");
		КонецЕсли;
		
		Если ОбычныеКаналыСвязиEMAILИспользование Тогда
			ОчередьПроверкиПодключенияОбычныеКаналыСвязи.Вставить("EMAIL");
		КонецЕсли;
		
	КонецЕсли;
	
	ПроверяемыйВидТранспорта = Неопределено;
	Для Каждого ЭлементПроверки Из ОчередьПроверкиПодключенияОбычныеКаналыСвязи Цикл
		ПроверяемыйВидТранспорта = ЭлементПроверки.Ключ;
		Прервать;
	КонецЦикла;
	
	ПродолжитьОжидание = Истина;
	ПриНачалеПроверкиПодключенияНаСервере(ПроверяемыйВидТранспорта, ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
			ПараметрыОбработчикаОжиданияПроверкиПодключения);
			
		ПодключитьОбработчикОжидания("ПриОжиданииПроверкиПодключенияOffline",
			ПараметрыОбработчикаОжиданияПроверкиПодключения.ТекущийИнтервал, Истина);
	Иначе
		ПриЗавершенииПроверкиПодключенияOffline();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОжиданииПроверкиПодключенияOffline()
	
	ПродолжитьОжидание = Ложь;
	ПриОжиданииПроверкиПодключенияНаСервере(ПараметрыОбработчикаПроверкиПодключения, ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ПодключитьОбработчикОжидания("ПриОжиданииПроверкиПодключенияOffline",
			ПараметрыОбработчикаОжиданияПроверкиПодключения.ТекущийИнтервал, Истина);
	Иначе
		ПараметрыОбработчикаОжиданияПроверкиПодключения = Неопределено;
		ПриЗавершенииПроверкиПодключенияOffline();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПроверкиПодключенияOffline()
	
	СообщениеОбОшибке = "";
	ПроверкаПодключенияВыполнена = Ложь;
	ПриЗавершенииПроверкиПодключенияНаСервере(ПроверкаПодключенияВыполнена, СообщениеОбОшибке);
	
	Если ПроверкаПодключенияВыполнена Тогда
		
		ПроверяемыйВидТранспорта = Неопределено;
		Для Каждого ЭлементПроверки Из ОчередьПроверкиПодключенияОбычныеКаналыСвязи Цикл
			ПроверяемыйВидТранспорта = ЭлементПроверки.Ключ;
			Прервать;
		КонецЦикла;
		ОчередьПроверкиПодключенияОбычныеКаналыСвязи.Удалить(ПроверяемыйВидТранспорта);
		
		Если ОчередьПроверкиПодключенияОбычныеКаналыСвязи.Количество() > 0 Тогда
			ПриНачалеПроверкиПодключенияOffline();
		Иначе
			ОчередьПроверкиПодключенияОбычныеКаналыСвязи = Неопределено;
			ИзменитьПорядковыйНомерПерехода(+1);
		КонецЕсли;
		
	Иначе
		
		ПроверяемыйВидТранспорта = Неопределено;
		ОчередьПроверкиПодключенияОбычныеКаналыСвязи = Неопределено;
		ИзменитьПорядковыйНомерПерехода(-1);
		
		Если Не ПустаяСтрока(СообщениеОбОшибке) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось подключиться к программе. Проверьте настройки подключения %1.'"), ПроверяемыйВидТранспорта));
		КонецЕсли;
				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриНачалеПроверкиПодключенияНаСервере(ВидТранспорта, ПродолжитьОжидание)
	
	СтруктураНастроек = Новый Структура;
	ЗаполнитьСтруктуруПараметровПодключенияПомощника(СтруктураНастроек);
	СтруктураНастроек.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена[ВидТранспорта];
	
	Обработки.ПомощникСозданияОбменаДанными.ПриНачалеПроверкиПодключения(
		СтруктураНастроек, ПараметрыОбработчикаПроверкиПодключения, ПродолжитьОжидание);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриОжиданииПроверкиПодключенияНаСервере(ПараметрыОбработчика, ПродолжитьОжидание)
	
	ПродолжитьОжидание = Ложь;
	Обработки.ПомощникСозданияОбменаДанными.ПриОжиданииПроверкиПодключения(ПараметрыОбработчика, ПродолжитьОжидание);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗавершенииПроверкиПодключенияНаСервере(ПроверкаПодключенияВыполнена, СообщениеОбОшибке = "")
	
	СтатусЗавершения = Неопределено;
	Обработки.ПомощникСозданияОбменаДанными.ПриЗавершенииПроверкиПодключения(
		ПараметрыОбработчикаПроверкиПодключения, СтатусЗавершения);
		
	Если СтатусЗавершения.Отказ Тогда
		ПроверкаПодключенияВыполнена = Ложь;
		СообщениеОбОшибке = СтатусЗавершения.СообщениеОбОшибке;
		Возврат;
	Иначе
		ПроверкаПодключенияВыполнена = СтатусЗавершения.Результат.ПодключениеУстановлено
			И СтатусЗавершения.Результат.ПодключениеРазрешено;
			
		Если Не ПроверкаПодключенияВыполнена
			И Не ПустаяСтрока(СтатусЗавершения.Результат.СообщениеОбОшибке) Тогда
			СообщениеОбОшибке = СтатусЗавершения.Результат.СообщениеОбОшибке;
		КонецЕсли;
			
		Если ПроверкаПодключенияВыполнена
			И СтатусЗавершения.Результат.ПараметрыКорреспондентаПолучены Тогда
			ЗаполнитьПараметрыКорреспондента(СтатусЗавершения.Результат.ПараметрыКорреспондента);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СохранениеНастроекПодключения

&НаКлиенте
Процедура ПриНачалеСохраненияНастроекПодключения()
	
	ПродолжитьОжидание = Истина;
	ПриНачалеСохраненияНастроекПодключенияНаСервере(ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(
			ПараметрыОбработчикаОжиданияСохраненияНастроекПодключения);
			
		ПодключитьОбработчикОжидания("ПриОжиданииСохраненияНастроекПодключения",
			ПараметрыОбработчикаОжиданияСохраненияНастроекПодключения.ТекущийИнтервал, Истина);
	Иначе
		ПриЗавершенииСохраненияНастроекПодключения();
	КонецЕсли;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПриОжиданииСохраненияНастроекПодключения()
	
	ПродолжитьОжидание = Ложь;
	ПриОжиданииСохраненияНастроекПодключенияНаСервере(МодельСервиса, ПараметрыОбработчикаСохраненияНастроекПодключения, ПродолжитьОжидание);
	
	Если ПродолжитьОжидание Тогда
		ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияСохраненияНастроекПодключения);
		
		ПодключитьОбработчикОжидания("ПриОжиданииСохраненияНастроекПодключения",
			ПараметрыОбработчикаОжиданияСохраненияНастроекПодключения.ТекущийИнтервал, Истина);
	Иначе
		ПриЗавершенииСохраненияНастроекПодключения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииСохраненияНастроекПодключения()
	
	НастройкиПодключенияСохранены = Ложь;
	СообщениеОбОшибке = "";
	АдресВХранилищеНастройкиПодключения = Неопределено;
	
	ПриЗавершенииСохраненияНастроекПодключенияНаСервере(НастройкиПодключенияСохранены, СообщениеОбОшибке);
	
	Если НастройкиПодключенияСохранены Тогда
		Если СохранитьПараметрыПодключенияВФайл
			И Не АдресВХранилищеНастройкиПодключения = Неопределено Тогда
			ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВХранилищеНастройкиПодключения);
			ДанныеФайла.Записать(ИмяФайлаНастроекПодключенияДляВыгрузки);
		КонецЕсли;
		
		ИзменитьПорядковыйНомерПерехода(+1);
		
		Оповестить("Запись_УзелПланаОбмена");
	Иначе
		ИзменитьПорядковыйНомерПерехода(-1);
		
		Если Не ПустаяСтрока(СообщениеОбОшибке) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеОбОшибке);
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Не удалось сохранить настройки подключения.'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриНачалеСохраненияНастроекПодключенияНаСервере(ПродолжитьОжидание)
	
	Если МодельСервиса И ПустаяСтрока(ВидПодключения) Тогда
		
		МодульОбменДаннымиВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиВМоделиСервиса");
		ИдентификаторИнформационнойБазыПриемника = МодульОбменДаннымиВМоделиСервиса.КодУзлаПланаОбменаВСервисе(
			ОбластьДанныхКорреспондента);
		
		НастройкиПодключения = Новый Структура;
		НастройкиПодключения.Вставить("ИмяПланаОбмена",               ИмяПланаОбменаКорреспондента);
		НастройкиПодключения.Вставить("ИмяПланаОбменаКорреспондента", ИмяПланаОбмена);
		
		НастройкиПодключения.Вставить("ФорматОбмена", ФорматОбмена);
		
		НастройкиПодключения.Вставить("Наименование", Наименование);
		НастройкиПодключения.Вставить("НаименованиеКорреспондента", НаименованиеКорреспондента);
		
		НастройкиПодключения.Вставить("Префикс", Префикс);
		НастройкиПодключения.Вставить("ПрефиксКорреспондента", ПрефиксКорреспондента);
		
		НастройкиПодключения.Вставить("ИдентификаторИнформационнойБазыИсточника", ИдентификаторИнформационнойБазыИсточника);
		НастройкиПодключения.Вставить("ИдентификаторИнформационнойБазыПриемника", ИдентификаторИнформационнойБазыПриемника);
		
		НастройкиПодключения.Вставить("КонечнаяТочкаКорреспондента", КонечнаяТочкаКорреспондента);

		НастройкиПодключения.Вставить("Корреспондент");
		НастройкиПодключения.Вставить("ОбластьДанныхКорреспондента", ОбластьДанныхКорреспондента);
		
		МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль(
			"Обработки.ПомощникНастройкиСинхронизацииДанныхМеждуПриложениямиВИнтернете");
	Иначе
		
		// Настройки подключения в формате структуры реквизитов помощника создания обмена данными.
		НастройкиПодключения = Новый Структура;
		ЗаполнитьСтруктуруПараметровПодключенияПомощника(НастройкиПодключения);
		
		МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль("Обработки.ПомощникСозданияОбменаДанными");
			
	КонецЕсли;
	
	ПараметрыОбработчикаСохраненияНастроекПодключения = Неопределено;
	МодульПомощникНастройки.ПриНачалеСохраненияНастроекПодключения(НастройкиПодключения,
		ПараметрыОбработчикаСохраненияНастроекПодключения, ПродолжитьОжидание);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПриОжиданииСохраненияНастроекПодключенияНаСервере(МодельСервиса, ПараметрыОбработчика, ПродолжитьОжидание)
	
	Если МодельСервиса Тогда
		МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль(
			"Обработки.ПомощникНастройкиСинхронизацииДанныхМеждуПриложениямиВИнтернете");
	Иначе
		МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль("Обработки.ПомощникСозданияОбменаДанными");
	КонецЕсли;
	
	ПродолжитьОжидание = Ложь;
	МодульПомощникНастройки.ПриОжиданииСохраненияНастроекПодключения(ПараметрыОбработчика, ПродолжитьОжидание);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗавершенииСохраненияНастроекПодключенияНаСервере(НастройкиПодключенияСохранены, СообщениеОбОшибке)
	
	СтатусЗавершения = Неопределено;
	
	Если МодельСервиса Тогда
		МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль(
			"Обработки.ПомощникНастройкиСинхронизацииДанныхМеждуПриложениямиВИнтернете");
	Иначе
		МодульПомощникНастройки = ОбщегоНазначения.ОбщийМодуль("Обработки.ПомощникСозданияОбменаДанными");
	КонецЕсли;
	
	МодульПомощникНастройки.ПриЗавершенииСохраненияНастроекПодключения(
		ПараметрыОбработчикаСохраненияНастроекПодключения, СтатусЗавершения);
	ПараметрыОбработчикаСохраненияНастроекПодключения = Неопределено;
		
	Если СтатусЗавершения.Отказ Тогда
		НастройкиПодключенияСохранены = Ложь;
		СообщениеОбОшибке = СтатусЗавершения.СообщениеОбОшибке;
		Возврат;
	Иначе
		НастройкиПодключенияСохранены = СтатусЗавершения.Результат.НастройкиПодключенияСохранены;
		
		Если Не НастройкиПодключенияСохранены Тогда
			СообщениеОбОшибке = СтатусЗавершения.Результат.СообщениеОбОшибке;
			Возврат;
		КонецЕсли;
		
		УзелОбмена = СтатусЗавершения.Результат.УзелОбмена;
		
		Если СохранитьПараметрыПодключенияВФайл Тогда
			ВременныйФайл = ПолучитьИмяВременногоФайла("xml");
			
			Запись = Новый ЗаписьТекста;
			Запись.Открыть(ВременныйФайл, "UTF-8");
			Запись.Записать(СтатусЗавершения.Результат.СтрокаНастроекПодключенияXML);
			Запись.Закрыть();
			
			АдресВХранилищеНастройкиПодключения = ПоместитьВоВременноеХранилище(
				Новый ДвоичныеДанные(ВременныйФайл), УникальныйИдентификатор);
				
			УдалитьФайлы(ВременныйФайл);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьСтруктуруПараметровПодключенияПомощника(СтруктураНастроекПомощника)
	
	// Преобразование структуры реквизитов формы в структуру реквизитов помощника.
	СтруктураНастроекПомощника.Вставить("ИмяПланаОбмена",               ИмяПланаОбмена);
	СтруктураНастроекПомощника.Вставить("ИмяПланаОбменаКорреспондента", ИмяПланаОбменаКорреспондента);
	
	СтруктураНастроекПомощника.Вставить("ВариантНастройкиОбмена", ИдентификаторНастройки);
	
	СтруктураНастроекПомощника.Вставить("ФорматОбмена", ФорматОбмена);
	
	Если ЗначениеЗаполнено(УзелОбмена) Тогда
		СтруктураНастроекПомощника.Вставить("Корреспондент", УзелОбмена);
	КонецЕсли;
	
	Если ПродолжениеНастройкиВПодчиненномУзлеРИБ
		Или ЗагрузитьПараметрыПодключенияИзФайла Тогда
		СтруктураНастроекПомощника.Вставить("ВариантРаботыМастера", "ПродолжитьНастройкуОбменаДанными");
	Иначе
		СтруктураНастроекПомощника.Вставить("ВариантРаботыМастера", "НастроитьНовыйОбменДанными");
	КонецЕсли;
	
	СтруктураНастроекПомощника.Вставить("СсылкаНового", Неопределено);
	
	СтруктураНастроекПомощника.Вставить("КодПредопределенногоУзла", ИдентификаторИнформационнойБазыИсточника);
		
	СтруктураНастроекПомощника.Вставить("КодНовогоУзлаВторойБазы", ИдентификаторИнформационнойБазыПриемника);
	СтруктураНастроекПомощника.Вставить("КодУзлаКорреспондента",   ИдентификаторИнформационнойБазыПриемника);
	
	СтруктураНастроекПомощника.Вставить("НаименованиеЭтойБазы",   Наименование);
	СтруктураНастроекПомощника.Вставить("НаименованиеВторойБазы", НаименованиеКорреспондента);
	
	СтруктураНастроекПомощника.Вставить("ПрефиксИнформационнойБазыИсточника", Префикс);
	СтруктураНастроекПомощника.Вставить("ПрефиксИнформационнойБазыПриемника", ПрефиксКорреспондента);
	
	СтруктураНастроекПомощника.Вставить("УзелИнформационнойБазы", УзелОбмена);
	
	СтруктураНастроекПомощника.Вставить("ИспользоватьПрефиксыДляНастройкиОбмена", ИспользоватьПрефиксыДляНастройкиОбмена);
	
	СтруктураНастроекПомощника.Вставить("ИдентификаторИнформационнойБазыИсточника", ИдентификаторИнформационнойБазыИсточника);
	СтруктураНастроекПомощника.Вставить("ИдентификаторИнформационнойБазыПриемника", ИдентификаторИнформационнойБазыПриемника);
	
	СтруктураНастроекПомощника.Вставить("ВерсияФорматаФайлаНастроекОбменаДанными",
		Обработки.ПомощникСозданияОбменаДанными.ВерсияФорматаНастроекОбменаДанными());
		
	СтруктураНастроекПомощника.Вставить("ВерсияФорматаОбмена", ВерсияФорматаОбмена);
	СтруктураНастроекПомощника.Вставить("ПоддерживаемыеОбъектыФормата", ПоддерживаемыеОбъектыФормата);
	
	// Настройки транспорта.	
	СтруктураНастроекПомощника.Вставить("COMАутентификацияОперационнойСистемы",
		ВнешнееСоединениеВидАутентификации = "ОперационнаяСистема");
	СтруктураНастроекПомощника.Вставить("COMВариантРаботыИнформационнойБазы",
		?(ВнешнееСоединениеВариантРаботыИнформационнойБазы = "Файловый", 0, 1));
	СтруктураНастроекПомощника.Вставить("COMИмяИнформационнойБазыНаСервере1СПредприятия",
		ВнешнееСоединениеИмяИнформационнойБазы);
	СтруктураНастроекПомощника.Вставить("COMИмяПользователя",
		ВнешнееСоединениеИмяПользователя);
	СтруктураНастроекПомощника.Вставить("COMИмяСервера1СПредприятия",
		ВнешнееСоединениеКластерСерверов);
	СтруктураНастроекПомощника.Вставить("COMКаталогИнформационнойБазы",
		ВнешнееСоединениеКаталогИнформационнойБазы);
	СтруктураНастроекПомощника.Вставить("COMПарольПользователя",
		ВнешнееСоединениеПароль);
		
	СтруктураНастроекПомощника.Вставить("EMAILМаксимальныйДопустимыйРазмерСообщения",
		ОбычныеКаналыСвязиEMAILМаксимальныйДопустимыйРазмерВложения);
	СтруктураНастроекПомощника.Вставить("EMAILСжиматьФайлИсходящегоСообщения",
		ОбычныеКаналыСвязиАрхивироватьФайлы);
	СтруктураНастроекПомощника.Вставить("EMAILУчетнаяЗапись",
		ОбычныеКаналыСвязиEMAILУчетнаяЗапись);
	СтруктураНастроекПомощника.Вставить("EMAILТранслитерироватьИменаФайловСообщенийОбмена",
		ОбычныеКаналыСвязиТранслитерироватьИменаФайлов);
	
	СтруктураНастроекПомощника.Вставить("FILEКаталогОбменаИнформацией",
		ОбычныеКаналыСвязиFILEКаталог);
	СтруктураНастроекПомощника.Вставить("FILEСжиматьФайлИсходящегоСообщения",
		ОбычныеКаналыСвязиАрхивироватьФайлы);
	СтруктураНастроекПомощника.Вставить("FILEТранслитерироватьИменаФайловСообщенийОбмена",
		ОбычныеКаналыСвязиТранслитерироватьИменаФайлов);
	
	СтруктураНастроекПомощника.Вставить("FTPСжиматьФайлИсходящегоСообщения",
		ОбычныеКаналыСвязиАрхивироватьФайлы);
	СтруктураНастроекПомощника.Вставить("FTPСоединениеМаксимальныйДопустимыйРазмерСообщения",
		ОбычныеКаналыСвязиFTPМаксимальныйДопустимыйРазмерФайла);
	СтруктураНастроекПомощника.Вставить("FTPСоединениеПароль",
		ОбычныеКаналыСвязиFTPПароль);
	СтруктураНастроекПомощника.Вставить("FTPСоединениеПассивноеСоединение",
		ОбычныеКаналыСвязиFTPПассивныйРежим);
	СтруктураНастроекПомощника.Вставить("FTPСоединениеПользователь",
		ОбычныеКаналыСвязиFTPПользователь);
	СтруктураНастроекПомощника.Вставить("FTPСоединениеПорт",
		ОбычныеКаналыСвязиFTPПорт);
	СтруктураНастроекПомощника.Вставить("FTPСоединениеПуть",
		ОбычныеКаналыСвязиFTPПуть);
	СтруктураНастроекПомощника.Вставить("FTPТранслитерироватьИменаФайловСообщенийОбмена",
		ОбычныеКаналыСвязиТранслитерироватьИменаФайлов);
		
	СтруктураНастроекПомощника.Вставить("WSURLВебСервиса", ИнтернетВебАдрес);
	СтруктураНастроекПомощника.Вставить("WSЗапомнитьПароль", ИнтернетЗапомнитьПароль);
	СтруктураНастроекПомощника.Вставить("WSИмяПользователя", ИнтернетИмяПользователя);
	СтруктураНастроекПомощника.Вставить("WSПароль", ИнтернетПароль);
	
	Если ВидПодключения = "Интернет" Тогда
		СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.WS);
	ИначеЕсли ВидПодключения = "ВнешнееСоединение" Тогда
		СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.COM);
	ИначеЕсли ВидПодключения = "ОбычныеКаналыСвязи" Тогда
		Если Не ЗначениеЗаполнено(ОбычныеКаналыСвязиВидТранспортаПоУмолчанию) Тогда
			Если ОбычныеКаналыСвязиFILEИспользование Тогда
				СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.FILE);
			ИначеЕсли ОбычныеКаналыСвязиFTPИспользование Тогда
				СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.FTP);
			ИначеЕсли ОбычныеКаналыСвязиEMAILИспользование Тогда
				СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.EMAIL);
			КонецЕсли;
		ИначеЕсли ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "FILE" Тогда
			СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.FILE);
		ИначеЕсли ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "FTP" Тогда
			СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.FTP);
		ИначеЕсли ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "EMAIL" Тогда
			СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.EMAIL);
		КонецЕсли;
	ИначеЕсли ВидПодключения = "ПассивныйРежим" Тогда
		СтруктураНастроекПомощника.Вставить("ВидТранспортаСообщенийОбмена", Перечисления.ВидыТранспортаСообщенийОбмена.WSПассивныйРежим);
	КонецЕсли;
	
	СтруктураНастроекПомощника.Вставить("ИспользоватьПараметрыТранспортаCOM",   ВидПодключения = "ВнешнееСоединение");
	
	СтруктураНастроекПомощника.Вставить("ИспользоватьПараметрыТранспортаEMAIL", ОбычныеКаналыСвязиEMAILИспользование);
	СтруктураНастроекПомощника.Вставить("ИспользоватьПараметрыТранспортаFILE",  ОбычныеКаналыСвязиFILEИспользование);
	СтруктураНастроекПомощника.Вставить("ИспользоватьПараметрыТранспортаFTP",   ОбычныеКаналыСвязиFTPИспользование);
	
	СтруктураНастроекПомощника.Вставить("ПарольАрхиваСообщенияОбмена", ОбычныеКаналыСвязиПарольАрхива);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСтруктуруПараметровПодключенияПомощника(СтруктураНастроекПомощника)
	
	// Преобразование структуры реквизитов помощника в структуру реквизитов формы.
	ИдентификаторИнформационнойБазыИсточника = СтруктураНастроекПомощника.КодПредопределенногоУзла;
	ИдентификаторИнформационнойБазыПриемника = СтруктураНастроекПомощника.КодНовогоУзлаВторойБазы;
	
	ИмяПланаОбменаКорреспондента = СтруктураНастроекПомощника.ИмяПланаОбменаКорреспондента;
	
	ИспользоватьПрефиксыДляНастройкиОбмена = (СтрДлина(ИдентификаторИнформационнойБазыПриемника) <> 36);
	
	Если ДоступноИзменениеНаименования Тогда
		Наименование = СтруктураНастроекПомощника.НаименованиеЭтойБазы;
	КонецЕсли;
	
	Если ДоступноИзменениеНаименованияКорреспондента Тогда
		НаименованиеКорреспондента = СтруктураНастроекПомощника.НаименованиеВторойБазы;
	КонецЕсли;
	
	Если ДоступноИзменениеПрефикса Тогда
		Префикс = СтруктураНастроекПомощника.ПрефиксИнформационнойБазыИсточника;
		Если ПустаяСтрока(Префикс)
			И ИспользоватьПрефиксыДляНастройкиОбмена Тогда
			Префикс = СтруктураНастроекПомощника.КодПредопределенногоУзла;
		КонецЕсли;
	КонецЕсли;
	
	Если ДоступноИзменениеПрефиксаКорреспондента Тогда
		ПрефиксКорреспондента = СтруктураНастроекПомощника.ПрефиксИнформационнойБазыПриемника;
		Если ПустаяСтрока(ПрефиксКорреспондента)
			И ИспользоватьПрефиксыДляНастройкиОбмена Тогда
			ПрефиксКорреспондента = СтруктураНастроекПомощника.КодНовогоУзлаВторойБазы;
		КонецЕсли;
	КонецЕсли;
	
	// Настройки транспорта.
	ВнешнееСоединениеВидАутентификации =
		?(СтруктураНастроекПомощника.COMАутентификацияОперационнойСистемы, "ОперационнаяСистема", "1СПредприятие");
	ВнешнееСоединениеВариантРаботыИнформационнойБазы =
		?(СтруктураНастроекПомощника.COMВариантРаботыИнформационнойБазы = 0, "Файловый", "КлиентСерверный");
	ВнешнееСоединениеИмяИнформационнойБазы =
		СтруктураНастроекПомощника.COMИмяИнформационнойБазыНаСервере1СПредприятия;
	ВнешнееСоединениеИмяПользователя =
		СтруктураНастроекПомощника.COMИмяПользователя;
	ВнешнееСоединениеКластерСерверов =
		СтруктураНастроекПомощника.COMИмяСервера1СПредприятия;
	ВнешнееСоединениеКаталогИнформационнойБазы =
		СтруктураНастроекПомощника.COMКаталогИнформационнойБазы;
	ВнешнееСоединениеПароль =
		СтруктураНастроекПомощника.COMПарольПользователя;
	
	ОбычныеКаналыСвязиEMAILМаксимальныйДопустимыйРазмерВложения =
		СтруктураНастроекПомощника.EMAILМаксимальныйДопустимыйРазмерСообщения;
	ОбычныеКаналыСвязиEMAILУчетнаяЗапись =
		СтруктураНастроекПомощника.EMAILУчетнаяЗапись;
	
	ОбычныеКаналыСвязиFILEКаталог =
		СтруктураНастроекПомощника.FILEКаталогОбменаИнформацией;
	
	ОбычныеКаналыСвязиFTPМаксимальныйДопустимыйРазмерФайла =
		СтруктураНастроекПомощника.FTPСоединениеМаксимальныйДопустимыйРазмерСообщения;
	ОбычныеКаналыСвязиFTPПароль =
		СтруктураНастроекПомощника.FTPСоединениеПароль;
	ОбычныеКаналыСвязиFTPПассивныйРежим =
		СтруктураНастроекПомощника.FTPСоединениеПассивноеСоединение;
	ОбычныеКаналыСвязиFTPПользователь =
		СтруктураНастроекПомощника.FTPСоединениеПользователь;
	ОбычныеКаналыСвязиFTPПорт =
		СтруктураНастроекПомощника.FTPСоединениеПорт;
	ОбычныеКаналыСвязиFTPПуть =
		СтруктураНастроекПомощника.FTPСоединениеПуть;
		
	ИнтернетВебАдрес        = СтруктураНастроекПомощника.WSURLВебСервиса;
	ИнтернетЗапомнитьПароль = СтруктураНастроекПомощника.WSЗапомнитьПароль;
	ИнтернетИмяПользователя = СтруктураНастроекПомощника.WSИмяПользователя;
	ИнтернетПароль          = СтруктураНастроекПомощника.WSПароль;
	
	Если СтруктураНастроекПомощника.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.WS Тогда
		ВидПодключения = "Интернет";
	ИначеЕсли СтруктураНастроекПомощника.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.COM Тогда
		ВидПодключения = "ВнешнееСоединение";
	ИначеЕсли СтруктураНастроекПомощника.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.FILE
		Или СтруктураНастроекПомощника.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.FTP
		Или СтруктураНастроекПомощника.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.EMAIL Тогда
		ВидПодключения = "ОбычныеКаналыСвязи";
	ИначеЕсли СтруктураНастроекПомощника.ВидТранспортаСообщенийОбмена = Перечисления.ВидыТранспортаСообщенийОбмена.WSПассивныйРежим Тогда
		ВидПодключения = "ПассивныйРежим";
	КонецЕсли;
	
	ОбычныеКаналыСвязиEMAILИспользование = СтруктураНастроекПомощника.ИспользоватьПараметрыТранспортаEMAIL;
	ОбычныеКаналыСвязиFILEИспользование  = СтруктураНастроекПомощника.ИспользоватьПараметрыТранспортаFILE;
	ОбычныеКаналыСвязиFTPИспользование   = СтруктураНастроекПомощника.ИспользоватьПараметрыТранспортаFTP;
	
	Если ОбычныеКаналыСвязиFILEИспользование Тогда
		ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "FILE";
	ИначеЕсли ОбычныеКаналыСвязиFTPИспользование Тогда
		ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "FTP";
	ИначеЕсли ОбычныеКаналыСвязиEMAILИспользование Тогда
		ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "EMAIL";
	КонецЕсли;
	
	ОбычныеКаналыСвязиТранслитерироватьИменаФайлов =
		СтруктураНастроекПомощника.FILEТранслитерироватьИменаФайловСообщенийОбмена
		Или СтруктураНастроекПомощника.FTPТранслитерироватьИменаФайловСообщенийОбмена
		Или СтруктураНастроекПомощника.EMAILТранслитерироватьИменаФайловСообщенийОбмена;
		
	ОбычныеКаналыСвязиАрхивироватьФайлы =
		СтруктураНастроекПомощника.FILEСжиматьФайлИсходящегоСообщения
		Или СтруктураНастроекПомощника.FTPСжиматьФайлИсходящегоСообщения
		Или СтруктураНастроекПомощника.EMAILСжиматьФайлИсходящегоСообщения;	
	
	ОбычныеКаналыСвязиПарольАрхива = СтруктураНастроекПомощника.ПарольАрхиваСообщенияОбмена;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыКорреспондента(ПараметрыКорреспондента)
	
	Если ЗначениеЗаполнено(ПараметрыКорреспондента.ПрефиксИнформационнойБазы) Тогда
		ПрефиксКорреспондента = ПараметрыКорреспондента.ПрефиксИнформационнойБазы;
	Иначе
		ПрефиксКорреспондента = ПараметрыКорреспондента.ПрефиксИнформационнойБазыПоУмолчанию;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыКорреспондента.НаименованиеИнформационнойБазы) Тогда
		НаименованиеКорреспондента = ПараметрыКорреспондента.НаименованиеИнформационнойБазы;
	Иначе
		НаименованиеКорреспондента = ПараметрыКорреспондента.НаименованиеИнформационнойБазыПоУмолчанию;
	КонецЕсли;
	
	ИдентификаторИнформационнойБазыПриемника = ПараметрыКорреспондента.КодЭтогоУзла;
	
	ВерсияКонфигурацииКорреспондента = ПараметрыКорреспондента.ВерсияКонфигурации;
	
	ИмяПланаОбменаКорреспондента = ПараметрыКорреспондента.ИмяПланаОбмена;
	
	Если НастройкаXDTO Тогда
		ИспользоватьПрефиксыДляНастройкиОбмена = ПараметрыКорреспондента.ИспользоватьПрефиксыДляНастройкиОбмена;
		
		ВерсияФорматаОбмена = ОбменДаннымиXDTOСервер.МаксимальнаяОбщаяВерсияФормата(
			ИмяПланаОбмена, ПараметрыКорреспондента.ВерсииФорматаОбмена);
		
		ПоддерживаемыеОбъектыФормата = Новый ХранилищеЗначения(
			ПараметрыКорреспондента.ПоддерживаемыеОбъектыФормата, Новый СжатиеДанных(9));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиПодключенияИзXMLНаСервере(АдресВХранилище, Отказ)
	
	ВременныйФайл = ПолучитьИмяВременногоФайла("xml");
		
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВХранилище);
	ДанныеФайла.Записать(ВременныйФайл);
	
	Попытка
		НастройкиПодключения = Обработки.ПомощникСозданияОбменаДанными.Создать();
		НастройкиПодключения.ИмяПланаОбмена = ИмяПланаОбмена;
		НастройкиПодключения.ВариантНастройкиОбмена = ИдентификаторНастройки;
		НастройкиПодключения.ВариантРаботыМастера = "ПродолжитьНастройкуОбменаДанными";
		
		Обработки.ПомощникСозданияОбменаДанными.ЗаполнитьНастройкиПодключенияИзXML(
			НастройкиПодключения, ВременныйФайл, Истина);
	Исключение
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Если ПустаяСтрока(СообщениеОбОшибке) Тогда
			СообщениеОбОшибке = НСтр("ru = 'Не удалось загрузить файл с настройками подключения.'");
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			СообщениеОбОшибке,
			, "ИмяФайлаНастроекПодключенияДляЗагрузки", , Отказ);
	КонецПопытки;
		
	УдалитьФайлы(ВременныйФайл);
	УдалитьИзВременногоХранилища(АдресВХранилище);
	
	Если Не Отказ Тогда
		ПрочитатьСтруктуруПараметровПодключенияПомощника(НастройкиПодключения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиXDTOКорреспондентаИзXMLНаСервере(АдресВХранилище, Отказ)
	
	ВременныйФайл = ПолучитьИмяВременногоФайла("xml");
		
	ДанныеФайла = ПолучитьИзВременногоХранилища(АдресВХранилище);
	ДанныеФайла.Записать(ВременныйФайл);
	
	Попытка
		НастройкиXDTOКорреспондента = Обработки.ПомощникСозданияОбменаДанными.НастройкиXDTOКорреспондентаИзXML(
			ВременныйФайл, Истина);
	Исключение
		Отказ = Истина;
		СообщениеОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(ОбменДаннымиСервер.СобытиеЖурналаРегистрацииСозданиеОбменаДанными(),
			УровеньЖурналаРегистрации.Ошибка, , , СообщениеОбОшибке);
			
		Возврат;
	КонецПопытки;

	УдалитьФайлы(ВременныйФайл);
	УдалитьИзВременногоХранилища(АдресВХранилище);
	
	ВерсияФорматаОбмена = ОбменДаннымиXDTOСервер.МаксимальнаяОбщаяВерсияФормата(ИмяПланаОбмена,
		НастройкиXDTOКорреспондента.ПоддерживаемыеВерсии);
		
	ТаблицаПоддерживаемыеОбъекты = НастройкиXDTOКорреспондента.ПоддерживаемыеОбъекты.Скопировать(
		Новый Структура("Версия", ВерсияФорматаОбмена));
		
	ПоддерживаемыеОбъектыФормата = Новый ХранилищеЗначения(ТаблицаПоддерживаемыеОбъекты,
		Новый СжатиеДанных(9));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеВидыТранспорта()
	
	ДоступныеВидыТранспорта = Новый Структура;
	
	ИспользуемыеТранспортыСообщенийОбмена = ОбменДаннымиПовтИсп.ИспользуемыеТранспортыСообщенийОбмена(
		ПланыОбмена[ИмяПланаОбмена].ПустаяСсылка(), ИдентификаторНастройки);
		
	Для Каждого ТекущийВидТранспорта Из ИспользуемыеТранспортыСообщенийОбмена Цикл
		// В модели сервиса обмен через Интернет и Интернет-пассивное соединение
		// поддерживается только для планов обмена XDTO.	
		Если МодельСервиса Тогда
			Если ТекущийВидТранспорта <> Перечисления.ВидыТранспортаСообщенийОбмена.WS
				И ТекущийВидТранспорта <> Перечисления.ВидыТранспортаСообщенийОбмена.WSПассивныйРежим Тогда
				Продолжить;
			КонецЕсли;
			
			Если Не НастройкаXDTO Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
			
		ДоступныеВидыТранспорта.Вставить(ОбщегоНазначения.ИмяЗначенияПеречисления(ТекущийВидТранспорта));
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПомещенияФайлаНастроекПодключенияДляЗагрузки(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	Если Не Результат Тогда
		Отказ = Истина;
	Иначе
		ЗаполнитьНастройкиПодключенияИзXMLНаСервере(Адрес, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось загрузить файл с настройками подключения.'"),
			, "ИмяФайлаНастроекПодключенияДляЗагрузки", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПомещенияФайлаНастроекXDTOКорреспондента(Результат, Адрес, ВыбранноеИмяФайла, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	Если Не Результат Тогда
		Отказ = Истина;
	Иначе
		ЗаполнитьНастройкиXDTOКорреспондентаИзXMLНаСервере(Адрес, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Не удалось загрузить файл с настройками корреспондента.'"),
			, "ИмяФайлаНастроекXDTOКорреспондента");
	КонецЕсли;
	
КонецПроцедуры

#Область ИнициализацияФормыПриСоздании

&НаСервере
Процедура ПроверитьВозможностьИспользованияФормы(Отказ = Ложь)
	
	// Обязательная должны быть переданы параметры помощника создания обмена данными.
	Если Не Параметры.Свойство("ИмяПланаОбмена")
		Или Не Параметры.Свойство("ИдентификаторНастройки") Тогда
		ТекстСообщения = НСтр("ru = 'Форма не предназначена для непосредственного использования.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьСвойстваФормы()
	
	Если НастройкаРИБ Тогда
		Заголовок = НСтр("ru = 'Настройка распределенной информационной базы'");
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настройка подключения к ""%1""'"),
			НаименованиеКонфигурацииКорреспондента);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьРеквизитыФормы()
	
	ИмяПланаОбмена         = Параметры.ИмяПланаОбмена;
	ИдентификаторНастройки = Параметры.ИдентификаторНастройки;
	
	ПродолжениеНастройкиВПодчиненномУзлеРИБ = Параметры.Свойство("ПродолжениеНастройкиВПодчиненномУзлеРИБ");
	
	ВерсияКонфигурацииКорреспондента = "";
	
	МодельСервиса = ОбщегоНазначения.РазделениеВключено()
		И ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных();
		
	ДоступнаНастройкаПараметровДоступаВИнтернет = ОбщегоНазначения.ПодсистемаСуществует(
		"СтандартныеПодсистемы.ПолучениеФайловИзИнтернета");
		
	НастройкаРИБ  = ОбменДаннымиПовтИсп.ЭтоПланОбменаРаспределеннойИнформационнойБазы(ИмяПланаОбмена);
	НастройкаXDTO = ОбменДаннымиПовтИсп.ЭтоПланОбменаXDTO(ИмяПланаОбмена);
	
	ЗаполнитьДоступныеВидыТранспорта();
	
	Если ДоступныеВидыТранспорта.Свойство("COM") Тогда
		ВидПодключения = "ВнешнееСоединение";
	ИначеЕсли ДоступныеВидыТранспорта.Свойство("WS") Тогда
		ВидПодключения = "Интернет";
	ИначеЕсли ДоступныеВидыТранспорта.Свойство("FILE")
		Или ДоступныеВидыТранспорта.Свойство("FTP")
		Или ДоступныеВидыТранспорта.Свойство("EMAIL") Тогда
		ВидПодключения = "ОбычныеКаналыСвязи";
	ИначеЕсли ДоступныеВидыТранспорта.Свойство("WSПассивныйРежим") Тогда
		ВидПодключения = "ПассивныйРежим";
	КонецЕсли;
	
	Если ДоступныеВидыТранспорта.Свойство("FILE") Тогда
		ОбычныеКаналыСвязиFILEИспользование  = Истина;
	ИначеЕсли ДоступныеВидыТранспорта.Свойство("FTP") Тогда
		ОбычныеКаналыСвязиFTPИспользование   = Истина;
	ИначеЕсли ДоступныеВидыТранспорта.Свойство("EMAIL") Тогда
		ОбычныеКаналыСвязиEMAILИспользование = Истина;
	КонецЕсли;
	
	Если ДоступныеВидыТранспорта.Свойство("FTP") Тогда
		ОбычныеКаналыСвязиFTPПорт = 21;
		ОбычныеКаналыСвязиFTPПассивныйРежим = Истина;
	КонецЕсли;
	
	СпособНастройкиПодключения = ?(МодельСервиса, "ПриложениеИзСписка", "НастроитьВручную");
	
	ЗначенияНастроекДляВарианта = ОбменДаннымиСервер.ЗначениеНастройкиПланаОбмена(ИмяПланаОбмена,
		"ИмяКонфигурацииКорреспондента,
		|ФорматОбмена,
		|ИмяФайлаНастроекДляПриемника,
		|НаименованиеКонфигурацииКорреспондента,
		|КраткаяИнформацияПоОбмену,
		|ПодробнаяИнформацияПоОбмену,
		|ИмяФормыПомощникаНастройкиСинхронизацииДанных",
		ИдентификаторНастройки);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗначенияНастроекДляВарианта);
	
	ИмяПланаОбменаКорреспондента = ИмяПланаОбмена;
	
	ДоступноИзменениеНаименования = Ложь;
	ДоступноИзменениеПрефикса     = Ложь;
	
	ДоступноИзменениеНаименованияКорреспондента = Истина;
	ДоступноИзменениеПрефиксаКорреспондента     = Истина;
	
	Если МодельСервиса Тогда
		МодульОбменДаннымиВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("ОбменДаннымиВМоделиСервиса");
		Наименование = МодульОбменДаннымиВМоделиСервиса.СформироватьНаименованиеПредопределенногоУзла();
	Иначе
		// Представление этой информационной базы.	
		Наименование = ОбменДаннымиСервер.НаименованиеПредопределенногоУзлаПланаОбмена(ИмяПланаОбмена);
		Если ПустаяСтрока(Наименование) Тогда
			ДоступноИзменениеНаименования = Истина;
			Наименование = ОбменДаннымиПовтИсп.ИмяЭтойИнформационнойБазы();
		КонецЕсли;
		
		НаименованиеКорреспондента = НаименованиеКонфигурацииКорреспондента;
	КонецЕсли;
	
	Префикс = ПолучитьФункциональнуюОпцию("ПрефиксИнформационнойБазы");
	Если ПустаяСтрока(Префикс) Тогда
		ДоступноИзменениеПрефикса = Истина;
		ОбменДаннымиПереопределяемый.ПриОпределенииПрефиксаИнформационнойБазыПоУмолчанию(Префикс);
	КонецЕсли;
	
	Если НастройкаРИБ Тогда
		ВидПодключения = "ОбычныеКаналыСвязи";
	КонецЕсли;
	
	Если ПродолжениеНастройкиВПодчиненномУзлеРИБ Тогда
		УзелОбмена = ОбменДаннымиСервер.ГлавныйУзел();
		
		// Заполнение параметров из настроек подключения в константе.
		ПомощникСозданияОбмена = Обработки.ПомощникСозданияОбменаДанными.Создать();
		ПомощникСозданияОбмена.ИмяПланаОбмена = ИмяПланаОбмена;
		ПомощникСозданияОбмена.ВариантНастройкиОбмена = ИдентификаторНастройки;
		ПомощникСозданияОбмена.ВариантРаботыМастера = "ПродолжитьНастройкуОбменаДанными";
		
		Обработки.ПомощникСозданияОбменаДанными.ЗаполнитьНастройкиПодключенияИзКонстанты(ПомощникСозданияОбмена);		
		ПрочитатьСтруктуруПараметровПодключенияПомощника(ПомощникСозданияОбмена);
		
		ДоступноИзменениеНаименованияКорреспондента = Ложь;
		ДоступноИзменениеПрефиксаКорреспондента     = Ложь;
	КонецЕсли;
	
	ИдентификаторИнформационнойБазыИсточника = ОбменДаннымиСервер.КодПредопределенногоУзлаПланаОбмена(ИмяПланаОбмена);
	
	// До получения настроек XDTO от корреспондента устанавливаем режим по-умолчанию.
	ИспользоватьПрефиксыДляНастройкиОбмена = Истина;
	
	ЗаполнитьТаблицуПереходов();
	
КонецПроцедуры

#КонецОбласти

#Область СценарииРаботыПомощника

&НаСервере
Функция ДобавитьСтрокуТаблицыПереходов(ИмяОсновнойСтраницы, ИмяСтраницыНавигации, ИмяСтраницыДекорации = "")
	
	СтрокаПереходов = ТаблицаПереходов.Добавить();
	СтрокаПереходов.ПорядковыйНомерПерехода = ТаблицаПереходов.Количество();
	СтрокаПереходов.ИмяОсновнойСтраницы = ИмяОсновнойСтраницы;
	СтрокаПереходов.ИмяСтраницыНавигации = ИмяСтраницыНавигации;
	СтрокаПереходов.ИмяСтраницыДекорации = ИмяСтраницыДекорации;
	
	Возврат СтрокаПереходов;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьТаблицуПереходов()
	
	ТаблицаПереходов.Очистить();
	
	Если НастройкаРИБ Тогда
		НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаПараметрыПодключенияОбычныеКаналыСвязи", "СтраницаНавигацияНачало");
		НовыйПереход.ИмяОбработчикаПриОткрытии = "СтраницаПараметрыПодключенияОбычныеКаналыСвязи_ПриОткрытии";
		НовыйПереход.ИмяОбработчикаПриПереходеДалее = "СтраницаПараметрыПодключенияОбычныеКаналыСвязи_ПриПереходеДалее";
	Иначе
		НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаСпособНастройкиПодключения", "СтраницаНавигацияНачало");
		НовыйПереход.ИмяОбработчикаПриОткрытии = "СтраницаСпособНастройкиПодключения_ПриОткрытии";
		НовыйПереход.ИмяОбработчикаПриПереходеДалее = "СтраницаСпособНастройкиПодключения_ПриПереходеДалее";
		
		НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаПараметрыПодключенияИнтернет", "СтраницаНавигацияПродолжение");
		НовыйПереход.ИмяОбработчикаПриОткрытии = "СтраницаПараметрыПодключенияИнтернет_ПриОткрытии";
		НовыйПереход.ИмяОбработчикаПриПереходеДалее = "СтраницаПараметрыПодключенияИнтернет_ПриПереходеДалее";
		
		Если Не МодельСервиса Тогда
			НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаПараметрыПодключенияВнешнееСоединение", "СтраницаНавигацияПродолжение");
			НовыйПереход.ИмяОбработчикаПриОткрытии = "СтраницаПараметрыПодключенияВнешнееСоединение_ПриОткрытии";
			НовыйПереход.ИмяОбработчикаПриПереходеДалее = "СтраницаПараметрыПодключенияВнешнееСоединение_ПриПереходеДалее";
			
			НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаПараметрыПодключенияОбычныеКаналыСвязи", "СтраницаНавигацияПродолжение");
			НовыйПереход.ИмяОбработчикаПриОткрытии = "СтраницаПараметрыПодключенияОбычныеКаналыСвязи_ПриОткрытии";
			НовыйПереход.ИмяОбработчикаПриПереходеДалее = "СтраницаПараметрыПодключенияОбычныеКаналыСвязи_ПриПереходеДалее";
		КонецЕсли;
	КонецЕсли;
	
	Если Не МодельСервиса Тогда
		НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаПроверкаПодключения", "СтраницаНавигацияПродолжение");
		НовыйПереход.ИмяОбработчикаПриОткрытии = "СтраницаПроверкаПодключения_ПриОткрытии";
		НовыйПереход.ДлительнаяОперация = Истина;
		НовыйПереход.ИмяОбработчикаДлительнойОперации = "СтраницаПроверкаПодключения_ДлительнаяОперация";
	КонецЕсли;
	
	НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаОбщиеНастройкиСинхронизации", "СтраницаНавигацияПродолжение");
	НовыйПереход.ИмяОбработчикаПриОткрытии = "СтраницаОбщиеНастройкиСинхронизации_ПриОткрытии";
	НовыйПереход.ИмяОбработчикаПриПереходеДалее = "СтраницаОбщиеНастройкиСинхронизации_ПриПереходеДалее";
	
	НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаСохранениеНастроекПодключения", "СтраницаНавигацияПродолжение");
	НовыйПереход.ДлительнаяОперация = Истина;
	НовыйПереход.ИмяОбработчикаДлительнойОперации = "СтраницаСохранениеНастроекПодключения_ДлительнаяОперация";
	
	НовыйПереход = ДобавитьСтрокуТаблицыПереходов("СтраницаОкончание", "СтраницаНавигацияОкончание");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиИзмененийРеквизитовФормы

&НаКлиенте
Процедура ПриИзмененииСпособНастройкиПодключения()
	
	Если СпособНастройкиПодключения = "ПриложениеИзСписка" Тогда
		Элементы.ПанельСпособыНастройкиПодключения.ТекущаяСтраница = Элементы.СтраницаМоиПриложения;
		
		НачатьПолучениеСпискаПриложенийДляПодключения();
	ИначеЕсли СпособНастройкиПодключения = "НастроитьВручную" Тогда
		
		Элементы.ВидПодключенияВнешнееСоединение.Видимость  = ДоступныеВидыТранспорта.Свойство("COM");
		Элементы.ВидПодключенияИнтернет.Видимость           = ДоступныеВидыТранспорта.Свойство("WS");
		Элементы.ВидПодключенияОбычныеКаналыСвязи.Видимость = ДоступныеВидыТранспорта.Свойство("FILE")
			Или ДоступныеВидыТранспорта.Свойство("FTP")
			Или ДоступныеВидыТранспорта.Свойство("EMAIL");
		Элементы.ВидПодключенияПассивныйРежим.Видимость     = ДоступныеВидыТранспорта.Свойство("WSПассивныйРежим");
		
		Элементы.ПанельСпособыНастройкиПодключения.ТекущаяСтраница = Элементы.СтраницаВидыПодключения;
		ПриИзмененииВидПодключения();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЗагрузитьНастройкиПодключенияИзФайла()
	
	Элементы.ИмяФайлаНастроекПодключенияДляЗагрузки.Доступность = ЗагрузитьПараметрыПодключенияИзФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВидПодключения()
	
	Если ВидПодключения = "ВнешнееСоединение" Тогда
		Элементы.ПанельФайлНастройки.Видимость = Ложь;
	ИначеЕсли ВидПодключения = "Интернет" Тогда
		Элементы.ПанельФайлНастройки.Видимость = Ложь;
	ИначеЕсли ВидПодключения = "ОбычныеКаналыСвязи" Тогда
		Элементы.ПанельФайлНастройки.Видимость = Истина;
		Элементы.ПанельФайлНастройки.ТекущаяСтраница = Элементы.СтраницаФайлНастройкиОбычныеКаналыСвязи;
		
		ПриИзмененииЗагрузитьНастройкиПодключенияИзФайла();
		
	ИначеЕсли ВидПодключения = "ПассивныйРежим" Тогда
		Элементы.ПанельФайлНастройки.Видимость = Истина;
		Элементы.ПанельФайлНастройки.ТекущаяСтраница = Элементы.СтраницаФайлНастройкиПассивныйРежим;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбычныеКаналыСвязиFILEИспользование()
	
	Элементы.ГруппаОбычныеКаналыСвязиFILEИспользование.Доступность = ОбычныеКаналыСвязиFILEИспользование;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПриИзмененииОбычныеКаналыСвязиFTPИспользование()
	
	Элементы.ГруппаОбычныеКаналыСвязиFTPИспользование.Доступность = ОбычныеКаналыСвязиFTPИспользование;
	ПриИзмененииОбычныеКаналыСвязиFTPИспользоватьОграничениеРазмераФайла();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбычныеКаналыСвязиEMAILИспользование()
	
	Элементы.ГруппаОбычныеКаналыСвязиEMAILИспользование.Доступность = ОбычныеКаналыСвязиEMAILИспользование;
	ПриИзмененииОбычныеКаналыСвязиEMAILИспользоватьОграничениеРазмераВложения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВнешнееСоединениеВариантРаботыИнформационнойБазы()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ВнешнееСоединениеКаталогИнформационнойБазы",
		"Доступность", ВнешнееСоединениеВариантРаботыИнформационнойБазы = "Файловый");
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ВнешнееСоединениеКластерСерверов",
		"Доступность", ВнешнееСоединениеВариантРаботыИнформационнойБазы = "КлиентСерверный");
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ВнешнееСоединениеИмяИнформационнойБазы",
		"Доступность", ВнешнееСоединениеВариантРаботыИнформационнойБазы = "КлиентСерверный");
	
КонецПроцедуры
	
&НаКлиенте
Процедура ПриИзмененииВнешнееСоединениеВидАутентификации()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ВнешнееСоединениеИмяПользователя",
		"Доступность", ВнешнееСоединениеВидАутентификации = "1СПредприятие");
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ВнешнееСоединениеПароль",
		"Доступность", ВнешнееСоединениеВидАутентификации = "1СПредприятие");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбычныеКаналыСвязиАрхивироватьФайлы()
	
	Элементы.ГруппаОбычныеКаналыСвязиПарольАрхива.Доступность = ОбычныеКаналыСвязиАрхивироватьФайлы;
	ПриИзмененииОбычныеКаналыСвязиИспользоватьПарольАрхива();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбычныеКаналыСвязиИспользоватьПарольАрхива()
	
	Элементы.ОбычныеКаналыСвязиПарольАрхива.Доступность = ОбычныеКаналыСвязиИспользоватьПарольДляАрхива;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбычныеКаналыСвязиFTPИспользоватьОграничениеРазмераФайла()
	
	Элементы.ОбычныеКаналыСвязиFTPМаксимальныйДопустимыйРазмерФайла.Доступность =
		ОбычныеКаналыСвязиFTPИспользоватьОграничениеРазмераФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОбычныеКаналыСвязиEMAILИспользоватьОграничениеРазмераВложения()
	
	Элементы.ОбычныеКаналыСвязиEMAILМаксимальныйДопустимыйРазмерВложения.Доступность = 
		ОбычныеКаналыСвязиEMAILИспользоватьОграничениеРазмераВложения;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПереходов

&НаКлиенте
Функция Подключаемый_СтраницаСпособНастройкиПодключения_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	Элементы.СпособНастройкиПодключения.Видимость = МодельСервиса
		И (ДоступныеВидыТранспорта.Свойство("WS") Или ДоступныеВидыТранспорта.Свойство("WSПассивныйРежим"));
		
	Если ЭтоПереходДалее Тогда
		ПриИзмененииСпособНастройкиПодключения();
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаСпособНастройкиПодключения_ПриПереходеДалее(Отказ)
	
	Если СпособНастройкиПодключения = "ПриложениеИзСписка" Тогда
		
		ТекущиеДанные = Элементы.ПриложенияВСервисе.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Выберите приложение из списка для продолжения настройки подключения.'"),
				, "ПриложенияВСервисе", , Отказ);
			Возврат 0;
		ИначеЕсли ТекущиеДанные.НастройкаСинхронизацииВМенеджереСервиса Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Для настройки синхронизации данных с выбранным приложением перейдите в менеджер сервиса.
				|В менеджере сервиса воспользуйтесь командой ""Синхронизация данных"".'"));
			Отказ = Истина;
			Возврат 0;
		Иначе
			ПрефиксОбласти = ТекущиеДанные.Префикс;
			
			НаименованиеКорреспондента   = ТекущиеДанные.НаименованиеПриложения;
			ПрефиксОбластиКорреспондента = ТекущиеДанные.ПрефиксКорреспондента;
			
			КонечнаяТочкаКорреспондента = ТекущиеДанные.КонечнаяТочкаКорреспондента;
			ОбластьДанныхКорреспондента = ТекущиеДанные.ОбластьДанных;
			
			ВидПодключения = "";
		КонецЕсли;
		
	ИначеЕсли СпособНастройкиПодключения = "НастроитьВручную" Тогда
		
		Если ВидПодключения = "ОбычныеКаналыСвязи"
			И ЗагрузитьПараметрыПодключенияИзФайла Тогда
			Если ПустаяСтрока(ИмяФайлаНастроекПодключенияДляЗагрузки) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Выберите файл с настройками подключения.'"),
					, "ИмяФайлаНастроекПодключенияДляЗагрузки", , Отказ);
				Возврат 0;
			Иначе
				// Загрузка настроек из файла.
				ОповещениеОЗавершении = Новый ОписаниеОповещения("ПриЗавершенииПомещенияФайлаНастроекПодключенияДляЗагрузки", ЭтотОбъект);
				НачатьПомещениеФайла(ОповещениеОЗавершении, , ИмяФайлаНастроекПодключенияДляЗагрузки, Ложь, УникальныйИдентификатор);
			КонецЕсли;
		ИначеЕсли ВидПодключения = "ПассивныйРежим" Тогда
			Если ПустаяСтрока(ИмяФайлаНастроекXDTOКорреспондента) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Выберите файл с настройками программы-корреспондента.'"),
					, "ИмяФайлаНастроекXDTOКорреспондента", , Отказ);
				Возврат 0;
			Иначе
				// Загрузка настроек корреспондента из файла.
				ОповещениеОЗавершении = Новый ОписаниеОповещения("ПриЗавершенииПомещенияФайлаНастроекXDTOКорреспондента", ЭтотОбъект);
				НачатьПомещениеФайла(ОповещениеОЗавершении, , ИмяФайлаНастроекXDTOКорреспондента, Ложь, УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПараметрыПодключенияИнтернет_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	Если ВидПодключения <> "Интернет" Тогда
		ПропуститьСтраницу = Истина;
		Возврат 0;
	КонецЕсли;
	
	Элементы.ПараметрыДоступаВИнтернет1.Видимость = ДоступнаНастройкаПараметровДоступаВИнтернет;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПараметрыПодключенияИнтернет_ПриПереходеДалее(Отказ)
	
	Если ВидПодключения <> "Интернет" Тогда
		Возврат 0;
	КонецЕсли;
	
	Если ПустаяСтрока(ИнтернетВебАдрес) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Укажите адрес программы в Интернет.'"),
			, "ИнтернетВебАдрес", , Отказ);
		Возврат 0;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПараметрыПодключенияВнешнееСоединение_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	Если МодельСервиса Тогда
		ПропуститьСтраницу = Истина;
	КонецЕсли;
	
	Если ВидПодключения <> "ВнешнееСоединение" Тогда
		ПропуститьСтраницу = Истина;
	КонецЕсли;
	
	ВнешнееСоединениеВариантРаботыИнформационнойБазы = "Файловый";
	ПриИзмененииВнешнееСоединениеВариантРаботыИнформационнойБазы();
	
	ВнешнееСоединениеВидАутентификации = "1СПредприятие";
	ПриИзмененииВнешнееСоединениеВидАутентификации();
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПараметрыПодключенияВнешнееСоединение_ПриПереходеДалее(Отказ)
	
	Если ВидПодключения <> "ВнешнееСоединение" Тогда
		Возврат 0;
	КонецЕсли;
	
	Если ВнешнееСоединениеВариантРаботыИнформационнойБазы = "Файловый" Тогда
		
		Если ПустаяСтрока(ВнешнееСоединениеКаталогИнформационнойБазы) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Выберите каталог расположения информационной базы.'"),
				, "ВнешнееСоединениеКаталогИнформационнойБазы", , Отказ);
			Возврат 0;
		КонецЕсли;
		
	ИначеЕсли ВнешнееСоединениеВариантРаботыИнформационнойБазы = "КлиентСерверный" Тогда
		
		Если ПустаяСтрока(ВнешнееСоединениеКластерСерверов) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Укажите имя кластера серверов 1С:Предприятия.'"),
				, "ВнешнееСоединениеКластерСерверов", , Отказ);
			Возврат 0;
		КонецЕсли;
		
		Если ПустаяСтрока(ВнешнееСоединениеИмяИнформационнойБазы) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Укажите имя информационной базы в кластере серверов 1С:Предприятия.'"),
				, "ВнешнееСоединениеИмяИнформационнойБазы", , Отказ);
			Возврат 0;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПараметрыПодключенияОбычныеКаналыСвязи_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	Если ВидПодключения <> "ОбычныеКаналыСвязи" Тогда
		ПропуститьСтраницу = Истина;
		Возврат 0;
	КонецЕсли;
		
	Элементы.ПараметрыДоступаВИнтернет.Видимость = ДоступнаНастройкаПараметровДоступаВИнтернет;
	
	Элементы.ГруппаСинхронизацияЧерезКаталог.Видимость = ДоступныеВидыТранспорта.Свойство("FILE");
	Элементы.ГруппаСинхронизацияЧерезFTP.Видимость = ДоступныеВидыТранспорта.Свойство("FTP");
	Элементы.ГруппаСинхронизацияЧерезEMAIL.Видимость = ДоступныеВидыТранспорта.Свойство("EMAIL");
	
	Если ЭтоПереходДалее Тогда
	
		ПриИзмененииОбычныеКаналыСвязиFILEИспользование();
		ПриИзмененииОбычныеКаналыСвязиFTPИспользование();
		ПриИзмененииОбычныеКаналыСвязиEMAILИспользование();
		
		ПриИзмененииОбычныеКаналыСвязиАрхивироватьФайлы();
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПараметрыПодключенияОбычныеКаналыСвязи_ПриПереходеДалее(Отказ)
	
	Если ВидПодключения <> "ОбычныеКаналыСвязи" Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ОбычныеКаналыСвязиFILEИспользование
		И Не ОбычныеКаналыСвязиFTPИспользование
		И Не ОбычныеКаналыСвязиEMAILИспользование Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Выберите хотя бы один способ передачи файлов с данными.'"),
			, "ОбычныеКаналыСвязиFILEИспользование", , Отказ);
		Возврат 0;
	КонецЕсли;
	
	Если ОбычныеКаналыСвязиАрхивироватьФайлы
		И ОбычныеКаналыСвязиИспользоватьПарольДляАрхива
		И ПустаяСтрока(ОбычныеКаналыСвязиПарольАрхива) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Укажите пароль для архивации файлов.'"),
			, "ОбычныеКаналыСвязиПарольАрхива", , Отказ);
		Возврат 0;
	КонецЕсли;
	
	Если ОбычныеКаналыСвязиFILEИспользование Тогда
		
		Если ПустаяСтрока(ОбычныеКаналыСвязиFILEКаталог) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Выберите каталог для передачи файлов с данными.'"),
				, "ОбычныеКаналыСвязиFILEКаталог", , Отказ);
			Возврат 0;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ОбычныеКаналыСвязиFTPИспользование Тогда
		
		Если ПустаяСтрока(ОбычныеКаналыСвязиFTPПуть) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Укажите адрес каталога для передачи файлов с данными.'"),
				, "ОбычныеКаналыСвязиFTPПуть", , Отказ);
			Возврат 0;
		КонецЕсли;
		
		Если ОбычныеКаналыСвязиFTPИспользоватьОграничениеРазмераФайла
			И Не ЗначениеЗаполнено(ОбычныеКаналыСвязиFTPМаксимальныйДопустимыйРазмерФайла) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Укажите максимальный допустимый размер файлов с данными.'"),
				, "ОбычныеКаналыСвязиFTPМаксимальныйДопустимыйРазмерФайла", , Отказ);
			Возврат 0;
		КонецЕсли;

	КонецЕсли;
	
	Если ОбычныеКаналыСвязиEMAILИспользование Тогда
		
		Если Не ЗначениеЗаполнено(ОбычныеКаналыСвязиEMAILУчетнаяЗапись) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Выберите учетную запись электронной почты для отправки сообщений с данными.'"),
				, "ОбычныеКаналыСвязиEMAILУчетнаяЗапись", , Отказ);
			Возврат 0;
		КонецЕсли;
		
		Если ОбычныеКаналыСвязиEMAILИспользоватьОграничениеРазмераВложения
			И Не ЗначениеЗаполнено(ОбычныеКаналыСвязиEMAILМаксимальныйДопустимыйРазмерВложения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Укажите максимальный допустимый размер почтового вложения.'"),
				, "ОбычныеКаналыСвязиEMAILМаксимальныйДопустимыйРазмерВложения", , Отказ);
			Возврат 0;
		КонецЕсли;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПроверкаПодключения_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	Если ВидПодключения = "ПассивныйРежим" Тогда
		
		ПроверкаПодключенияВыполнена = Истина;
		ПропуститьСтраницу = Истина;
		
	ИначеЕсли ВидПодключения = "ВнешнееСоединение"
		Или ВидПодключения = "Интернет" Тогда
		
		Элементы.ПанельПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаПодключенияOnline;
		
	ИначеЕсли ВидПодключения = "ОбычныеКаналыСвязи" Тогда
		
		Элементы.ПанельПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаПодключенияOffline;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаПроверкаПодключения_ДлительнаяОперация(Отказ, ПерейтиДалее)
	
	ПерейтиДалее = Ложь;
	
	Если ВидПодключения = "ПассивныйРежим" Тогда
		
		ПерейтиДалее = Истина;
		
	ИначеЕсли ВидПодключения = "ВнешнееСоединение"
		Или ВидПодключения = "Интернет" Тогда
		
		ПриНачалеПроверкиПодключенияOnline();
		
	ИначеЕсли ВидПодключения = "ОбычныеКаналыСвязи" Тогда
		
		ПриНачалеПроверкиПодключенияOffline();
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаОбщиеНастройкиСинхронизации_ПриОткрытии(Отказ, ПропуститьСтраницу, ЭтоПереходДалее)
	
	Если ЭтоПереходДалее Тогда
		Элементы.ОбычныеКаналыСвязиВидТранспортаПоУмолчанию.СписокВыбора.Очистить();
		
		Если ОбычныеКаналыСвязиFILEИспользование Тогда
			Элементы.ОбычныеКаналыСвязиВидТранспортаПоУмолчанию.СписокВыбора.Добавить("FILE");
			ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "FILE";
		КонецЕсли;
		
		Если ОбычныеКаналыСвязиFTPИспользование Тогда
			Элементы.ОбычныеКаналыСвязиВидТранспортаПоУмолчанию.СписокВыбора.Добавить("FTP");
			Если Не ЗначениеЗаполнено(ОбычныеКаналыСвязиВидТранспортаПоУмолчанию) Тогда
				ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "FTP";
			КонецЕсли;
		КонецЕсли;
		
		Если ОбычныеКаналыСвязиEMAILИспользование Тогда
			Элементы.ОбычныеКаналыСвязиВидТранспортаПоУмолчанию.СписокВыбора.Добавить("EMAIL");
			Если Не ЗначениеЗаполнено(ОбычныеКаналыСвязиВидТранспортаПоУмолчанию) Тогда
				ОбычныеКаналыСвязиВидТранспортаПоУмолчанию = "EMAIL";
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ОбычныеКаналыСвязиВидТранспортаПоУмолчанию.Видимость =
		(Элементы.ОбычныеКаналыСвязиВидТранспортаПоУмолчанию.СписокВыбора.Количество() > 1);
		
	СохранитьПараметрыПодключенияВФайл = Не НастройкаРИБ
		И (ВидПодключения = "ОбычныеКаналыСвязи") И Не ЗагрузитьПараметрыПодключенияИзФайла;
	
	Элементы.ИмяФайлаНастроекПодключенияДляВыгрузки.Видимость = СохранитьПараметрыПодключенияВФайл;
			
	Если СохранитьПараметрыПодключенияВФайл
		И ОбычныеКаналыСвязиFILEИспользование Тогда
		ИмяФайлаНастроекПодключенияДляВыгрузки = ОбщегоНазначенияКлиентСервер.ПолучитьПолноеИмяФайла(
			ОбычныеКаналыСвязиFILEКаталог, ИмяФайлаНастроекДляПриемника + ".xml");
	КонецЕсли;
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"Наименование", "ТолькоПросмотр", Не ДоступноИзменениеНаименования);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"Префикс", "ТолькоПросмотр", Не ДоступноИзменениеПрефикса);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"НаименованиеКорреспондента", "ТолькоПросмотр", Не ДоступноИзменениеНаименованияКорреспондента);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"ПрефиксКорреспондента", "ТолькоПросмотр", Не ДоступноИзменениеПрефиксаКорреспондента);
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаОбщиеНастройкиСинхронизации_ПриПереходеДалее(Отказ)
	
	//
	
КонецФункции

&НаКлиенте
Функция Подключаемый_СтраницаСохранениеНастроекПодключения_ДлительнаяОперация(Отказ, ПерейтиДалее)
	
	ПерейтиДалее = Ложь;
	ПриНачалеСохраненияНастроекПодключения();
	
КонецФункции

#КонецОбласти

#Область ДополнительныеОбработчикиПереходов

&НаКлиенте
Процедура ИзменитьПорядковыйНомерПерехода(Итератор)
	
	ОчиститьСообщения();
	
	УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + Итератор);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПорядковыйНомерПерехода(Знач Значение)
	
	ЭтоПереходДалее = (Значение > ПорядковыйНомерПерехода);
	
	ПорядковыйНомерПерехода = Значение;
	
	Если ПорядковыйНомерПерехода < 1 Тогда
		
		ПорядковыйНомерПерехода = 1;
		
	КонецЕсли;
	
	ПорядковыйНомерПереходаПриИзменении(ЭтоПереходДалее);
	
КонецПроцедуры

&НаКлиенте
Процедура ПорядковыйНомерПереходаПриИзменении(Знач ЭтоПереходДалее)
	
	// Выполняем обработчики событий перехода.
	ВыполнитьОбработчикиСобытийПерехода(ЭтоПереходДалее);
	
	// Устанавливаем отображение страниц.
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	Элементы.ПанельОсновная.ТекущаяСтраница  = Элементы[СтрокаПереходаТекущая.ИмяОсновнойСтраницы];
	Элементы.ПанельНавигации.ТекущаяСтраница = Элементы[СтрокаПереходаТекущая.ИмяСтраницыНавигации];
	
	Элементы.ПанельНавигации.ТекущаяСтраница.Доступность = Не (ЭтоПереходДалее И СтрокаПереходаТекущая.ДлительнаяОперация);
	
	// Устанавливаем текущую кнопку по умолчанию.
	КнопкаДалее = ПолучитьКнопкуФормыПоИмениКоманды(Элементы.ПанельНавигации.ТекущаяСтраница, "КомандаДалее");
	
	Если КнопкаДалее <> Неопределено Тогда
		
		КнопкаДалее.КнопкаПоУмолчанию = Истина;
		
	Иначе
		
		КнопкаГотово = ПолучитьКнопкуФормыПоИмениКоманды(Элементы.ПанельНавигации.ТекущаяСтраница, "КомандаГотово");
		
		Если КнопкаГотово <> Неопределено Тогда
			
			КнопкаГотово.КнопкаПоУмолчанию = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоПереходДалее И СтрокаПереходаТекущая.ДлительнаяОперация Тогда
		
		ПодключитьОбработчикОжидания("ВыполнитьОбработчикДлительнойОперации", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработчикиСобытийПерехода(Знач ЭтоПереходДалее)
	
	// Обработчики событий переходов.
	Если ЭтоПереходДалее Тогда
		
		СтрокиПерехода = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода - 1));
		
		Если СтрокиПерехода.Количество() > 0 Тогда
			СтрокаПерехода = СтрокиПерехода[0];
		
			// Обработчик ПриПереходеДалее.
			Если Не ПустаяСтрока(СтрокаПерехода.ИмяОбработчикаПриПереходеДалее)
				И Не СтрокаПерехода.ДлительнаяОперация Тогда
				
				ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ)";
				ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПерехода.ИмяОбработчикаПриПереходеДалее);
				
				Отказ = Ложь;
				
				Результат = Вычислить(ИмяПроцедуры);
				
				Если Отказ Тогда
					
					УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
					
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		СтрокиПерехода = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода + 1));
		
		Если СтрокиПерехода.Количество() > 0 Тогда
			СтрокаПерехода = СтрокиПерехода[0];
		
			// Обработчик ПриПереходеНазад.
			Если Не ПустаяСтрока(СтрокаПерехода.ИмяОбработчикаПриПереходеНазад)
				И Не СтрокаПерехода.ДлительнаяОперация Тогда
				
				ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ)";
				ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПерехода.ИмяОбработчикаПриПереходеНазад);
				
				Отказ = Ложь;
				
				Результат = Вычислить(ИмяПроцедуры);
				
				Если Отказ Тогда
					
					УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
					
					Возврат;
					
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	Если СтрокаПереходаТекущая.ДлительнаяОперация И Не ЭтоПереходДалее Тогда
		
		УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
		Возврат;
	КонецЕсли;
	
	// обработчик ПриОткрытии
	Если Не ПустаяСтрока(СтрокаПереходаТекущая.ИмяОбработчикаПриОткрытии) Тогда
		
		ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ, ПропуститьСтраницу, ЭтоПереходДалее)";
		ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПереходаТекущая.ИмяОбработчикаПриОткрытии);
		
		Отказ = Ложь;
		ПропуститьСтраницу = Ложь;
		
		Результат = Вычислить(ИмяПроцедуры);
		
		Если Отказ Тогда
			
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
			
			Возврат;
			
		ИначеЕсли ПропуститьСтраницу Тогда
			
			Если ЭтоПереходДалее Тогда
				
				УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
				
				Возврат;
				
			Иначе
				
				УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработчикДлительнойОперации()
	
	СтрокиПереходаТекущие = ТаблицаПереходов.НайтиСтроки(Новый Структура("ПорядковыйНомерПерехода", ПорядковыйНомерПерехода));
	
	Если СтрокиПереходаТекущие.Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru = 'Не определена страница для отображения.'");
	КонецЕсли;
	
	СтрокаПереходаТекущая = СтрокиПереходаТекущие[0];
	
	// Обработчик ОбработкаДлительнойОперации.
	Если Не ПустаяСтрока(СтрокаПереходаТекущая.ИмяОбработчикаДлительнойОперации) Тогда
		
		ИмяПроцедуры = "Подключаемый_[ИмяОбработчика](Отказ, ПерейтиДалее)";
		ИмяПроцедуры = СтрЗаменить(ИмяПроцедуры, "[ИмяОбработчика]", СтрокаПереходаТекущая.ИмяОбработчикаДлительнойОперации);
		
		Отказ = Ложь;
		ПерейтиДалее = Истина;
		
		Результат = Вычислить(ИмяПроцедуры);
		
		Если Отказ Тогда
			
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода - 1);
			
			Возврат;
			
		ИначеЕсли ПерейтиДалее Тогда
			
			УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
			
			Возврат;
			
		КонецЕсли;
		
	Иначе
		
		УстановитьПорядковыйНомерПерехода(ПорядковыйНомерПерехода + 1);
		
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьКнопкуФормыПоИмениКоманды(ЭлементФормы, ИмяКоманды)
	
	Для Каждого Элемент Из ЭлементФормы.ПодчиненныеЭлементы Цикл
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			
			ЭлементФормыПоИмениКоманды = ПолучитьКнопкуФормыПоИмениКоманды(Элемент, ИмяКоманды);
			
			Если ЭлементФормыПоИмениКоманды <> Неопределено Тогда
				
				Возврат ЭлементФормыПоИмениКоманды;
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Элемент) = Тип("КнопкаФормы")
			И СтрНайти(Элемент.ИмяКоманды, ИмяКоманды) > 0 Тогда
			
			Возврат Элемент;
			
		Иначе
			
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти